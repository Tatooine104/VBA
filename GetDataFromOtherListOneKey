Sub GetDataFromOtherList1(ByVal SourseList As String, ByVal SourseColName1 As String, ByVal SourseColName2 As String, ByVal SourseRowNumb As Long, ByVal DestColName1 As String, ByVal DestColName2 As String, ByVal DestRowNumb As Long)
'Скрипт переноса данных на активный лист с другого листа по простому ключу
'
' - + - + - + - + - + - + [Берем данные с листа источника] - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - +
With Worksheets(SourseList)
    ' Находим колонку с ключом
    SourseColNumb1 = .Range(SourseRowNumb & ":" & SourseRowNumb).Find(SourseColName1, , , xlWhole).Column
    ' Находим колонку с данными
    SourseColNumb2 = .Range(SourseRowNumb & ":" & SourseRowNumb).Find(SourseColName2, , , xlWhole).Column
    ' Определяем колиество строк
    SourseRowsCount = .Range(.Cells(SourseRowNumb, SourseColNumb1), .Cells(1048576, SourseColNumb1)).Find("", , , xlWhole).Row - 1
    ' Превращаем колонку с ключами в массив
    SourseArray1 = .Range(.Cells(SourseRowNumb + 1, SourseColNumb1), .Cells(SourseRowsCount, SourseColNumb1))
    ' Превращаем колонку с данными в массив
    SourseArray2 = .Range(.Cells(SourseRowNumb + 1, SourseColNumb2), .Cells(SourseRowsCount, SourseColNumb2))
End With

' - + - + - + - + - + - + [Инициализируем словарь] - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - +
With CreateObject("scripting.dictionary"): .comparemode = 1

' - + - + - + - + - + - + [Отправляем данные из источника в словарь] - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - +
    For i = LBound(SourseArray1) To UBound(SourseArray1)
        ' Новая запись в словаре
        If Not .exists("!!!" & CStr(SourseArray1(i, 1))) Then
            .Item("!!!" & CStr(SourseArray1(i, 1))) = CStr(SourseArray2(i, 1))
        ' Запись уже есть
        Else
            If InStr(.Item("!!!" & CStr(SourseArray1(i, 1))), CStr(SourseArray2(i, 1))) = 0 Then .Item("!!!" & CStr(SourseArray1(i, 1))) = .Item("!!!" & CStr(SourseArray1(i, 1))) & "; " & CStr(SourseArray2(i, 1))
        End If
    Next i
    
' - + - + - + - + - + - + [Берем данные с листа места назначения] - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - +
    DestColNumb1 = Range(DestRowNumb & ":" & DestRowNumb).Find(DestColName1, , , xlWhole).Column
    DestColNumb2 = Range(DestRowNumb & ":" & DestRowNumb).Find(DestColName2, , , xlWhole).Column
    DestRowsCount = Range(Cells(DestRowNumb, DestColNumb1), Cells(1048576, DestColNumb1)).Find("", , , xlWhole).Row - 1
    DestArray1 = Range(Cells(DestRowNumb + 1, DestColNumb1), Cells(DestRowsCount, DestColNumb1))
    DestArray2 = Range(Cells(DestRowNumb + 1, DestColNumb2), Cells(DestRowsCount, DestColNumb2))
    
' - + - + - + - + - + - + [Извлекам данные] - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - +
    For i = LBound(DestArray1) To UBound(DestArray1)
        If .exists("!!!" & CStr(DestArray1(i, 1))) Then DestArray2(i, 1) = .Item("!!!" & CStr(DestArray1(i, 1)))
    Next i
    
' - + - + - + - + - + - + [Стираем словарь] - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - +
    .RemoveAll
    
End With

' - + - + - + - + - + - + [Вставляем данные] - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - +
Range(Cells(DestRowNumb + 1, DestColNumb2), Cells(DestRowsCount, DestColNumb2)) = DestArray2

' - + - + - + - + - + - + [Стираем массив] - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - + - +
Erase SourseArray1, SourseArray2, DestArray1, DestArray2

End Sub
